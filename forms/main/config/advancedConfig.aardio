import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=892;bottom=709;bgcolor=16777215)
winform.add(
btnUpdate={cls="plus";text="保 存";left=457;top=603;right=560;bottom=639;align="left";bgcolor=11580047;db=1;dl=1;font=LOGFONT(h=-13);iconStyle={align="left";font=LOGFONT(h=-13;name='FontAwesome');padding={left=22}};iconText='\uF0C7';notify=1;textPadding={left=40};z=5};
checkLan={cls="plus";text="允许来自局域网的连接";left=328;top=449;right=535;bottom=480;align="left";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF0C8';notify=1;textPadding={left=19};z=1};
chkEnableGitConfigGithub={cls="plus";text="设置 git 客户端启用 github.com 代理提速（支持https、ssh协议）";left=328;top=489;right=844;bottom=520;align="left";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF0C8';notify=1;textPadding={left=19};z=11};
chkSystemStart={cls="plus";text="允许开机启动 winXray";left=328;top=529;right=844;bottom=560;align="left";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);iconStyle={align="left";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF0C8';notify=1;textPadding={left=19};z=15};
editHttpPort={cls="plus";left=330;top=243;right=410;bottom=267;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};db=1;dl=1;editable="edit";font=LOGFONT(h=-16);num=1;textPadding={bottom=1};z=12};
editLanPacUrl={cls="plus";left=332;top=395;right=751;bottom=419;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};db=1;dl=1;editable="edit";font=LOGFONT(h=-16);textPadding={bottom=1};z=9};
editPacPort={cls="plus";left=330;top=294;right=410;bottom=318;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};db=1;dl=1;editable="edit";font=LOGFONT(h=-16);num=1;textPadding={bottom=1};z=2};
editPacUrl={cls="plus";left=330;top=346;right=749;bottom=370;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};db=1;dl=1;editable="edit";font=LOGFONT(h=-16);textPadding={bottom=1};z=7};
editSocksPort={cls="plus";left=330;top=197;right=410;bottom=221;align="left";bgcolor=16777215;border={bottom=1;color=-8355712};db=1;dl=1;editable="edit";font=LOGFONT(h=-16);num=1;textPadding={bottom=1};z=3};
static={cls="static";text="SOCKS 代理服务端口：";left=120;top=203;right=318;bottom=229;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=4};
static2={cls="static";text="PAC 端口：";left=188;top=299;right=318;bottom=325;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=6};
static3={cls="static";text="本机 PAC 地址：";left=161;top=351;right=319;bottom=377;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=8};
static4={cls="static";text="局域网 PAC 地址：";left=163;top=400;right=321;bottom=426;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=10};
static5={cls="static";text="HTTP/HTTPS 代理服务端口：";left=60;top=249;right=318;bottom=275;align="right";bgcolor=16777215;db=1;dl=1;font=LOGFONT(h=-16);transparent=1;z=13};
static6={cls="static";text="支持 SOCKS4、SOCKS4A、SOCKS5";left=418;top=203;right=705;bottom=229;bgcolor=16777215;color=5921370;db=1;dl=1;font=LOGFONT(h=-13);transparent=1;z=14}
)
/*}}*/

import style;
winform.checkLan.skin(style.checkBox);  
winform.chkEnableGitConfigGithub.skin(style.checkBox);  
winform.chkSystemStart.skin(style.checkBox); 
winform.btnUpdate.skin(style.primaryButton);
winform.editSocksPort.skin(style.edit);
winform.editPacPort.skin(style.edit);
winform.editPacUrl.editBox.readonly = true;

import xray.pacServer;
onPacUpdated = function(){
	winform.editPacPort.text = xray.pacServer.getPort() || config.proxy.pacPort || 1083;
	winform.editPacUrl.text = xray.pacServer.getUrl();
	if(winform.checkLan.checked){
		winform.editLanPacUrl.text = xray.pacServer.getUrl(true);
	}
	winform.editLanPacUrl.disabled = !winform.checkLan.checked;
}

subscribe("pacServer.restarted",function(pacPort){
    onPacUpdated()
}) 

import xray.core;
var onInboundsUpdated = function(){
	var inbounds = ..config.core.default[["inbounds"]];
	winform.editSocksPort.text = xray.core.socksProxyPort || inbounds[[1]][["port"]] || 1081;
	winform.editHttpPort.text = xray.core.httpProxyPort || inbounds[[2]][["port"]] || 1082;
	winform.checkLan.checked = inbounds[["inbounds"]][["listen"]] !== "127.0.0.1"
}

subscribe("config.core.changed",function(){
    onInboundsUpdated();
})  

subscribe("xrayCore.restarted",function(socksProxyPort,httpProxPort){
    onInboundsUpdated();
})  

import win.reg;
var reg = win.reg("HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Run\");
winform.chkSystemStart.checked = reg.queryValue("winXray");
reg.close();

winform.chkEnableGitConfigGithub.checked = config.proxy.enableGitConfigGithub;
onInboundsUpdated();
onPacUpdated();

import wsock.tcp.client;
winform.btnUpdate.oncommand = function(id,event){
	winform.btnUpdate.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250'};
	
	var client = wsock.tcp.client();  
	var oldPort = xray.core.socksProxyPort;
	if( tonumber(winform.editSocksPort.text)
		&& tonumber(winform.editSocksPort.text) != oldPort
		&& client.connectTimeout("127.0.0.1",tonumber(winform.editSocksPort.text) ,0.5) ){
		winform.editSocksPort.editBox.showErrorTip(,"端口已被占用");
		winform.btnUpdate.disabledText = null;
		return client.close(); 
	} 
	client.close();  
	
	var client = wsock.tcp.client();  
	var oldPacPort = xray.pacServer.getPort();
	if( tonumber(winform.editPacPort.text)
		&& tonumber(winform.editPacPort.text) != oldPacPort
		&& client.connectTimeout("127.0.0.1",tonumber(winform.editPacPort.text) ,0.5) ){
		winform.editPacPort.editBox.showErrorTip(,"PAC端口已被占用");
		winform.btnUpdate.disabledText = null;
		return client.close(); 
	} 
	client.close(); 
	
  
	if(winform.chkEnableGitConfigGithub.checked!=config.proxy.enableGitConfigGithub){
		config.proxy.enableGitConfigGithub = winform.chkEnableGitConfigGithub.checked;
			
		import xray.github;
		if(!config.proxy.enableGitConfigGithub){
			xray.github.setProxy(false);		
		} 
		else {
			xray.github.setProxy(null);	
		}
	} 
	
	var restartCore = false;
	var port = tonumber(winform.editSocksPort.text) 
	config.core.default.inbounds[1]["port"]  = port;
	if(port!=oldPort){ 
		restartCore = true;
	}	
	
	var port = tonumber(winform.editHttpPort.text) 
	config.core.default.inbounds[2]["port"]  = port;
	if(port!=oldPort){
		restartCore = true;
	}		

	var listen = winform.checkLan.checked ? "0.0.0.0" : "127.0.0.1"; 
	if(listen!=config.core.default.inbounds[1]["listen"]){
		config.core.default.inbounds[1]["listen"]= listen;
		restartCore = true;
	}		 
    
    config.core.save();
    publish("config.inbounds.changed");
    if(restartCore) {
		publish("uiCommand.restartXrayCore");    
	}
	
	config.proxy.pacPort  = tonumber(winform.editPacPort.text)
	if(oldPacPort!=tonumber(winform.editPacPort.text)){ 
		xray.pacServer.restart();
	}
	
	sysProxy.reset(true);
    
    import win.reg;
    var reg = win.reg("HKEY_CURRENT_USER\Software\Microsoft\Windows\CurrentVersion\Run\");
    if(!_STUDIO_INVOKED){
    	if(winform.chkSystemStart.checked){
    		reg.setSzValue("winXray",`"`+io._exepath+`" /tray`);
    	}
    	else {
    		reg.delValue("winXray");
    	}           	
    } 
    reg.close();
    
    winform.btnUpdate.disabledText = null;
	winform.msgOk("配置已更新",1200);
}

winform.editPacUrl.editBox.readonly = true;
winform.editLanPacUrl.editBox.readonly = true;

import win.clip;
import wsock.tcp.client;
winform.editSocksPort.editBox.enablePopMenu(function(){
    var lanIp = wsock.tcp.client.getLocalIp("www.baidu.com");
	return {
			{ "复制端口" + winform.editSocksPort.text;
				function(id){ 
					win.clip.write("" + winform.editSocksPort.text)	
				}
			};
			{ "复制本机IP与端口 127.0.0.1:" + winform.editSocksPort.text;
				function(id){ 
					win.clip.write("127.0.0.1:" + winform.editSocksPort.text)	
				}
			}; 
			{ "复制局域网IP与端口 "+lanIp+":" + winform.editSocksPort.text;
				function(id){ 
					win.clip.write(lanIp+":" + winform.editSocksPort.text)
				}
			}; 
	}
} )

winform.editHttpPort.editBox.enablePopMenu(function(){
    var lanIp = wsock.tcp.client.getLocalIp("www.baidu.com");
	return {
			{ "复制端口" + winform.editHttpPort.text;
				function(id){ 
					win.clip.write("" + winform.editHttpPort.text)	
				}
			};
			{ "复制本机IP与端口 127.0.0.1:" + winform.editHttpPort.text;
				function(id){ 
					win.clip.write("127.0.0.1:" + winform.editHttpPort.text)	
				}
			}; 
			{ "复制局域网IP与端口 "+lanIp+":" + winform.editHttpPort.text;
				function(id){ 
					win.clip.write(lanIp+":" + winform.editHttpPort.text)
				}
			}; 
	}
} )

winform.editPacUrl.setCueBannerText("当前未切换到PAC代理模式");
winform.editLanPacUrl.setCueBannerText("当前未切换到PAC代理模式");

winform.checkLan.oncommand = function(id,event){
	winform.editLanPacUrl.disabled = !winform.checkLan.checked
}

winform.show();
win.loopMessage();
return winform;